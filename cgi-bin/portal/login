#!/usr/bin/perl
#
# login
#	Formulario de ingreso al portal de servicios
#
# 2016-06-13. Ernesto - Versión 1.0

$::debug = 0;
($::debug) and print "Content-type:text/plain\n\n";


use strict;
use DBI;
use Template;
use CGI qw/:standard/;
#use utf8;
use CGI::Carp qw/fatalsToBrowser/;
use Net::LDAP;
use MIME::Base64;
use portal3 qw/dbGet dbInsert dbConnect error userInfo checkFormat updateSession portal_url newSession validatePassword newSecret validateSecret addlog_db/;

$::ambiente = (param('ambiente') eq 'preproduccion' ? 'preproduccion' : 'produccion');

my ($error,$userid,$sessionid,$dependid,$lugarid) = portal3::_getSession(cookie(-name=>'SESION'));

my $redirect = checkFormat(param('r'), '[\w\d_.?=/%-]+');
($redirect) or $redirect = checkFormat(decode_base64(checkFormat(param('r64'), '[\w\d_=/%\r\n]+')), '[\w\d_.?=/%:-]+');

if ($sessionid && $error eq "OK") {
	# Ya hay una sesión válida
	updateSession($sessionid);

	if ($redirect) {
		print redirect( $redirect );
	} else {
		print redirect( portal_url("principales") );
	}
	exit(0);
}


#
# proceso el formulario inical
#

my $page = Template->new({EXPOSE_BLOCKS => 1});

my %tvars;

my $user = checkFormat(param('userid'), 'u\d+');
my $passwd = checkFormat(param('password'), '[a-zA-Z0-9áéíóúñäëïöüÁÉÍÓÚÑÄËÏÖÜçÇ,;.:ºª!|"@·#$~%&¬/()=?¿¡+*{} 	_-]+');
my $secret = checkFormat(param('secret'), '\d+:\d+:\d+');

if ($user && $passwd && $secret) {
	if (validateSecret($secret)) {
		my $err = validatePassword($user, $passwd);
		if ($err == 0) {
			my $sessionid = newSession($user);
			if ($sessionid) {
				my $deletecookie = cookie(-name=>'SESION', -value=>'', -path=>'/', -domain=>'ces.edu.uy', -expires=>'-1d');
				my $cookie = cookie(-name=>'SESION', -value=>$sessionid);
				print header(-charset=>'utf-8',-cookie=>[$deletecookie,$cookie],-status=>"302 Found",
				             -location=>($redirect ? $redirect : portal_url("principales")));
				addlog_db($user,1,"Usuario $user , Ingreso exitoso");
				exit(0);
			}
			# si falla la creación de la sesión vuelvo al formulario
			$tvars{mensaje} = "No se puede abrir la sesión en el Portal. Reintente luego";
		} elsif ($err == 1) {
			# error de base de datos
			$tvars{mensaje} = "Se ha producido un error inesperado. Reintente luego";
		} elsif ($err == 2) {
			# usuario no habilitado
			$tvars{mensaje} = "Su usuario no está habilitado. Comuníquese con Mesa de Ayuda";
		} elsif ($err == 3) {
			# muchos errores
			$tvars{mensaje} = "Se ha equivocado demasiadas veces. Debe esperar un minuto antes de reintentar el ingreso";
			$tvars{error_usr_pwd} = 1;
		} elsif ($err == 4) {
			# la password no coincide
			$tvars{mensaje} = "Usuario o contraseña incorrecta";
			$tvars{error_usr_pwd} = 1;
			$tvars{userid} = $user;
		}
	} else {
		# el secret caducó o no es válido
		$tvars{mensaje} = "El formulario está vencido. Reintente el ingreso";
	}
}

{ local $/ = undef; if (open(FILE,"/var/www/html/portal/imagenes/descripcion.html")) { $tvars{descripcion}=<FILE>; close(FILE); } };

# determino el ambiente:
$tvars{ambiente} =  $::ambiente;

$tvars{secret} = newSecret();
$tvars{redirect64} = encode_base64($redirect);

$tvars{background} = "/portal/portal/imagenes/background.jpg?".(stat("/var/www/html/portal/imagenes/background.jpg"))[9] ;

# Genero el HTML final:
print header(-charset=>'utf-8');

$page->process("login.tt", \%tvars)
|| die "Template process failed: ", $page->error(), "\n";

exit (0);
